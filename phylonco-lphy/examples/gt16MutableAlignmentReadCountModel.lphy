data {
n = 20;
l = 20;
meanW = 2.2;
sdW = 1.25;
alphaEpsilon = 1;
betaEpsilon = 30;
alphaDelta = 3;
betaDelta = 40;
meanT = 1.702; // average coverage = lognormal(meanT, sdT)
sdT = 0.8671;
meanV = 0.8; // variance coverage = lognormal(meanV, sdV)
sdV = 0.475;
meanS = 0.2; // cell-specific scaling
sdS = 0.72;
}

model {
Θ ~ LogNormal(meanlog=-2.0, sdlog=1.0);
ψ ~ Coalescent(n=n, theta=Θ);
π ~ Dirichlet(conc=[3.0,3.0,3.0,3.0,3.0,3.0,3.0,3.0,3.0,3.0,3.0,3.0,3.0,3.0,3.0,3.0]);
rates ~ Dirichlet(conc=[1.0, 2.0, 1.0, 1.0, 2.0, 1.0]);
Q = gt16(freq=π, rates=rates);
A ~ PhyloCTMC(L=l, Q=Q, tree=ψ, dataType=phasedGenotype());
w ~ LogNormal(meanlog= meanW, sdlog= sdW);
epsilon ~ Beta(alpha= alphaEpsilon, beta= betaEpsilon);
delta ~ Beta(alpha= alphaDelta, beta= betaDelta);
t ~ LogNormal(meanlog= meanT, sdlog= sdT);
v ~ LogNormal(meanlog= meanV, sdlog= sdV);
s ~ LogNormal(meanlog= meanS, sdlog= sdS, replicates=n);
alpha ~ Ploidy(l= l, n= n, delta= delta);
cov ~ CoverageModel(alpha= alpha, t= t, v= v, s= s);
r ~ ReadCountModel(D=A, epsilon=epsilon, alpha=alpha, coverage=cov, w=w);
}