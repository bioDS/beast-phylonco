

N_max ~ LogNormal(meanlog=4.5, sdlog=2.0);
f0 ~ Beta(alpha=1, beta=1);
b ~ LogNormal(meanlog=3, sdlog=1.0);
t50 ~ LogNormal(meanlog=-2.2, sdlog=0.8);
NA ~ LogNormal(meanlog=4.0, sdlog=2.0);
I_na ~ UniformDiscrete(lower=0, upper=1);

constantPopFunc = constantPopFunc(N0=N_max);
exponentialPopFunc = exponentialPopFunc(GrowthRate=b, N0=N_max, NA=NA, I_na=I_na);
logisticPopFunc = logisticPopFunc(t50=t50, nCarryingCapacity=N_max, b=b, NA=NA, I_na=I_na);
gompertzPopFunc = gompertzPopFunc_f0(N0=N_max,f0=f0, b=b, NA=NA, I_na=I_na);



models = [constantPopFunc, exponentialPopFunc,logisticPopFunc, gompertzPopFunc];
I ~ UniformDiscrete(lower=0, upper=length(models)-1);
SVSmodel = stochasticVariableSelection(indicator=I, models=models);
tree ~CoalescentPopFunc(n=40, popFunc=SVSmodel);
κ ~ LogNormal(meanlog=1.0, sdlog=0.5);
π ~ Dirichlet(conc=[2.0,2.0,2.0,2.0]);
D ~ PhyloCTMC(L=500, Q=hky(kappa=κ, freq=π), mu=0.00079, tree=tree);
height = tree.rootAge();
length = tree.treeLength();
